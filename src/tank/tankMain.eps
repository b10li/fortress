import util.utilEud;
import tank.tankAim;
import tank.tankBullet;

const unitTank = 5;
const loc1 = $L('loc1');
const tankEpd = EUDArray(6);

function newTank(targetPlayer)
{
	tankEpd[targetPlayer] = epdread_epd(EPD(0x628438));
	CreateUnit(1, unitTank, loc1+1, targetPlayer);

	tankAim.setAngle(tankEpd[targetPlayer], 100); //init
}

function getTankEpd(targetPlayer)
{
	return tankEpd[targetPlayer];
}

function controlTank(targetPlayer)
{// 키인식으로 MurakamiShiinaQC.py
    if(CountdownTimer(Exactly, 0))
    {
        const angleNum = 10; //단위
        const gaugeUnit = 47; //unitid
        const deathUnit = 0;

        const unitEpd = getTankEpd(targetPlayer);
        const energy = utilEud.getUnitEnergy(unitEpd);
        const angle = tankAim.getAngle(unitEpd);

        // check angle with Ore
        SetResources(0, SetTo, angle, Ore);
        const locID = $L('loc_tank');
        const orderID = utilEud.getOrderID(unitEpd);
        const death = utilEud.getDeath(targetPlayer, deathUnit);

        if(orderID == 6)  // while moving
        	tankAim.clearAngle(targetPlayer);

        if(1 == death)
        {// angle UP
            tankAim.setAngle(unitEpd, angle + angleNum);
            tankAim.showLaunchAngle(unitEpd);
        }
        else if(2 == death)
        {// angle DOWN
            tankAim.setAngle(unitEpd, angle - angleNum);
            tankAim.showLaunchAngle(unitEpd);
        }
        else if (4 == death && Accumulate(targetPlayer, Exactly, 0, Gas))
        {// shoot inital key press
            //initialize 
            SetResources(targetPlayer, SetTo, 1, Gas);
            SetMemoryEPD(unitEpd + 0xA0 / 4, SetTo, 0);
            SetDeaths(targetPlayer, SetTo, 0, 217);
        }
        else if (4 != death && Accumulate(targetPlayer, Exactly, 1, Gas))
        {// end key press 
            SetDeaths(targetPlayer, Add, 1, 217);
        }
        else if (4 == death && Accumulate(targetPlayer, Exactly, 1, Gas))
        {// shoot key pressing
            SetMemoryEPD(unitEpd + 0xA0 / 4, Add, 3*0x1000000);
        }
        if (Deaths(targetPlayer, AtLeast, 2, 217) || energy == 200)
        {// end key press for 1sec
            SetDeaths(targetPlayer, SetTo, 0, 217);
            SetResources(targetPlayer, SetTo, 0, Gas);
            tankAim.clearAngle(targetPlayer);
            tankBullet.shoot(unitEpd, angle, energy);
            SetMemoryEPD(unitEpd + 0xA0 / 4, SetTo, 0);
            // cooldown
            SetCountdownTimer(SetTo, 3);
        }
    }
}

