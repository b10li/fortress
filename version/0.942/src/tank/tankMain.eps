import header;
import util.utilEud;
import tank.tankAim;
import tank.tankBullet;
import tank.tankSelect;
import physics;
import customText3 as ct; 

const tankEpd = EUDArray(6);
const locList = [$L('p1'), $L('p2'), $L('p3'), $L('p4'), $L('p5'), $L('p6')];


// 기모으다가 시간 끝나면생기는 버그   
function toggleDisable(index);
function newTank(targetPlayer, unitType)
{
    if(unitType)
    {
        const locID = locList[targetPlayer];

        tankEpd[targetPlayer] = epdread_epd(EPD(0x628438));
        CreateUnit(1, unitType, locID+1, targetPlayer);
        //initialize
        SetMemoryEPD(tankEpd[targetPlayer]+ 0x034 / 4, SetTo, 200); //speed
        utilEud.setRemainingBuildtime(tankEpd[targetPlayer], header.maxbuildtime); // power
        tankAim.setAngle(tankEpd[targetPlayer], 300); //angle
        physics.setVxy(tankEpd[targetPlayer], 0, 10); //gravity
        // change ui
        const bullet = tankBullet.getBullet(unitType, utilEud.getDeath(targetPlayer, header.bulletQ));
        utilEud.setBuildQueue1(tankEpd[targetPlayer], bullet);

        toggleDisable(targetPlayer, 0);
        CenterView(locID+1);
    }

}

function removeTank(targetPlayer)
{
    tankEpd[targetPlayer] = 0;
}
function getTankEpd(targetPlayer)
{
	return tankEpd[targetPlayer];
}

function checkDeath(targetPlayer)
{
    if(Deaths(targetPlayer, Exactly, 1, utilEud.getUnitType(tankEpd[targetPlayer])))
        return 1;
    else
        return 0;
}
function controlTank(targetPlayer)
{// 키인식으로 
    const angleNum = 4; //단위
    const unitEpd = getTankEpd(targetPlayer);
    const power = utilEud.getRemainingBuildtime(unitEpd);
    const angle = tankAim.getAngle(unitEpd);

    const locID = $L('loc_tank');
    const orderID = utilEud.getOrderID(unitEpd);
    const keyValue = utilEud.getDeath(targetPlayer, header.keydeath);

    if(orderID == 6)  // while moving
    	tankAim.clearAngle(targetPlayer);

    if(1 == keyValue)
    {// angle UP
    	utilEud.setOrderID(unitEpd, 107);
        tankAim.setAngle(unitEpd, angle + angleNum);
        tankAim.showLaunchAngle(unitEpd);
    }
    else if(2 == keyValue)
    {// angle DOWN
    	utilEud.setOrderID(unitEpd, 107);
        tankAim.setAngle(unitEpd, angle - angleNum);
        tankAim.showLaunchAngle(unitEpd);
    }
    else if (4 == keyValue && Switch(targetPlayer, Cleared))
    {// shoot inital key press
        //initialize 
        utilEud.setRemainingBuildtime(unitEpd, header.maxbuildtime);
        SetSwitch(targetPlayer, Set);
        //key delay
        SetDeaths(targetPlayer, SetTo, 0, header.jKeyDelay);
    }
    else if (4 != keyValue && Switch(targetPlayer, Set))
    {// shoot end key press 
        SetDeaths(targetPlayer, Add, 1, header.jKeyDelay);
    }
    else if (4 == keyValue && Switch(targetPlayer, Set))
    {// shoot while pressing
        utilEud.setRemainingBuildtime(unitEpd, power -2);
        SetResources(targetPlayer, SetTo, 101-power, Gas);
        SetDeaths(targetPlayer, SetTo, 0, header.jKeyDelay);
    }
    else if (8 == keyValue && Switch(targetPlayer+6, Cleared))
    {// change weapon
        //const chatPtr = dwread_epd(EPD(0x640B58));
        ct.printP(targetPlayer, ""); // ct.cp는 모두에게 출력
        ct.printP(targetPlayer, "\x13\x07[ \x0e무기 변경\x07 ] \x01");
        //dwwrite_epd(EPD(0x640B58), chatPtr);
        SetSwitch(targetPlayer+6, Set);
        //L key delay
        SetDeaths(targetPlayer, SetTo, 6, header.lKeyDelay);
        tankBullet.toggleBullet(unitEpd);
    }

    if(8 != keyValue && Switch(targetPlayer+6, Set))
    {// change weapon keydelay
    	SetDeaths(targetPlayer, Subtract, 1, header.lKeyDelay);
        //SetSwitch(targetPlayer+6, Clear);
    }
    else if(8 == keyValue && Switch(targetPlayer+6, Set))
    {// change weapon keydelay
    	SetDeaths(targetPlayer, SetTo, 6, header.lKeyDelay);
    }
    
    if(Deaths(targetPlayer, Exactly, 0, header.lKeyDelay) && Switch(targetPlayer+6, Set))
    {// change weapon keydelay
    	SetSwitch(targetPlayer+6, Clear);
    }
    if (Deaths(targetPlayer, AtLeast, 8, header.jKeyDelay) || power == 0)
    {// shoot key delay
        utilEud.setRemainingBuildtime(unitEpd, header.maxbuildtime);
        SetDeaths(targetPlayer, SetTo, 0, header.jKeyDelay);
        SetSwitch(targetPlayer, Clear);
        tankAim.clearAngle(targetPlayer);
        // temp storage
        utilEud.setKillCount(unitEpd, (header.maxbuildtime+1) - power); 
        return unitEpd;
    }
    else 
        return 0;
    
}
function reduceStasis(unitEpd);
function renderTank()
{//tank physics
    for(var i=0; i<6; i++)
    {
        const unitEpd = tankEpd[i];
        if(unitEpd)
        {
            const unitType = utilEud.getUnitType(unitEpd);
            const player = utilEud.getPlayerID(unitEpd);
            if(!utilEud.isAtLocation(unitEpd, $L('PlayArea')))
            {
                tankEpd[i] = 0;
                KillUnit(unitType, player);
                RemoveUnit('(any unit)', player);
            }
            else if(player == $P12)
            {
                tankEpd[i] = 0;
                RemoveUnit('(any unit)', $P12);
            }
            MoveLocation($L('locTank')+1, unitType, i, 'Anywhere');
            //collision
            if(Bring($P8, Exactly, 0, '(buildings)', $L('locTank')+1))
            {
                physics.renderUnit(unitEpd, 0);
            }
        }
    }
}

function randomSelect()
{
    for(var i=0; i<6; i++){
        if(Command(i, Exactly, 1, header.Flag) && !tankSelect.isListed(utilEud.getUnitType(tankEpd[i]))){
            SetDeaths(i, SetTo, header.List[(rand()%header.ListNum) * 3], header.tankType);
            RemoveUnit(header.Flag, i);
        }
    }
}
function selectTank()
{
    if(tankSelect.isSelecting())
    {
        tankSelect.selectScreen();
        return 0;
    }
    else
    {
        EUDPlayerLoop()();
        const p = getcurpl();
        if(p<6)
        {
            const tankType = utilEud.getDeath(p, header.tankType);
            if(tankType) // 0 or sth
            {
                newTank(p, tankType);
            }     
        }
        EUDEndPlayerLoop();
        return 1;
    }
}

function shootMissile(index)
{
    tankBullet.shoot(tankEpd[index]);
}

function toggleDisable(index, status)
{
    const unitEpd = tankEpd[index];
    const statusFlag = dwread_epd(unitEpd + 0x0DC / 4);
    const disableFlag = 0x1000;
    utilEud.setOrderID(unitEpd, 107); //hold
    if(statusFlag == (statusFlag | disableFlag) && status == 1)
    {// isDisabled -> enable

        SetMemoryEPD(unitEpd + 0x0DC / 4, Subtract, disableFlag);
    }
    else if(status == 0)
    {// isNormal -> disable
        SetMemoryEPD(unitEpd + 0x0DC / 4, SetTo, statusFlag | disableFlag);
    }
}
