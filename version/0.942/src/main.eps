/* Fortress Star.eps -------------------------
this code follows BSD license.

madeby: sheun_t_@naver.com (우마)
used:
SCM draft2
EUD Editor
EUD Draft
CustomText
http://cafe.naver.com/edac/64676
etc.
//a무기 설명, 약한 바람
---------------------------------------------*/
import header;
import physics;
import eudEdit;
import tank.tankMain;
import tank.tankAim;
import tank.tankBullet;
import util.utilEud;
import sound.emplayer;
import customText3 as ct; 

//0 if test
const RELEASE = 1;
const turnArray = [0,1,2, 3,4,5,7];
var turnPointer = 6;
var gameState = 0;
var preTurn = 7;
var wind = 0;
function onPluginStart() 
{
	eudEdit.memoryEdit();
	randomize();
}
function displayWind(windVal);
function gameInfo(targetPlayer);
function getPlayerText(turnPlayer);
function getRandomTurn();
function playerDeath();
function teamVictory();
function indiVictory();
function beforeTriggerExec() 
{
	const turnPlayer = turnArray[turnPointer];
	if(gameState == 0)
	{//start select
		SetCountdownTimer(SetTo, 40);
		gameState = 11;
	}
	if(gameState == 11)
	{
		if(CountdownTimer(Exactly, 0))
			tankMain.randomSelect();
		if(tankMain.selectTank())
		{
			gameState = 12;
		}
	}
	if(gameState == 12)
	{
		if(getRandomTurn());
			gameState = 13;
	}
	if(gameState == 13)
	{
		if(!RELEASE) 
		{//create dummy
			for(var i=1; i<6; i++)
				CreateUnit(1, 15, tankMain.locList[i]+1, $P8);
		}
		if(Bring($Force1, AtLeast, 1, '(men)', $L('PlayArea')))
			gameState = 20;
	}
	if(gameState == 20)
	{//control

		if(CountdownTimer(Exactly, 0))
		{//pre control
			if(preTurn == turnPlayer && RELEASE) 
			{// time end
				//remove info
				SetDeaths(turnPlayer, SetTo, 0, header.jKeyDelay);
        		SetResources(turnPlayer, SetTo, 0, Gas);
				SetDeaths(turnPlayer, SetTo, 0, header.shootNum);
				tankMain.toggleDisable(turnPlayer, 0);
				//get next turn
				gameState = 40;
				return;
			}
			if(turnPlayer !=7 && playerexist(turnPlayer))
			{
				//remove pre info
				SetSwitch(preTurn, Clear);
				SetDeaths(preTurn, SetTo, 0, header.jKeyDelay);
				SetDeaths(preTurn, SetTo, 0, header.shootNum);
				// random wind
				wind = (rand()%44)/4;
				// clear map
				RemoveUnitAt(All, '(men)', $L('PlayArea')+1, $P7);
				SetCountdownTimer(SetTo, 40);
				tankMain.toggleDisable(turnPlayer, 1);
				// center view
				MoveLocation($L('loc1')+1, '(men)', turnPlayer, $L('PlayArea')+1);
				utilEud.CenterViewAll($L('loc1'));
			}
			else 
			{// dead or none player
				turnArray[turnPointer] = 7;
				//get next turn
				gameState = 40;
			}

		}
		if(CountdownTimer(AtLeast, 1))
		{//start control
			preTurn = turnPlayer;
			//start texing
			emplayer.timeAlert();
			const chatPtr = dwread_epd(EPD(0x640B58));
			displayWind(wind);
			getPlayerText(turnPlayer);
			ct.printP(ct.cp, "\x13\x01차례");
			gameInfo(turnPlayer);
			const check = tankMain.controlTank(turnPlayer);
			if (check)
			{//goto shoot state
				gameState = 30;
				SetCountdownTimer(SetTo, 0);
				//shoot chance
				SetDeaths(turnPlayer, SetTo, 1, header.shootNum);
				//disable
				tankMain.toggleDisable(turnPlayer, 0);
			}
			//end of texting
			dwwrite_epd(EPD(0x640B58), chatPtr);
		}
	}
	if(gameState == 30)
	{// shoot
		const shootChance = utilEud.getDeath(turnPlayer, header.shootNum);
		if(shootChance > 0 && CountdownTimer(Exactly, 0))
		{
			SetCountdownTimer(SetTo, 9);
			tankMain.shootMissile(turnPlayer);
			SetDeaths(turnPlayer, Subtract, 1, header.shootNum);
		}
		else if(shootChance == 0 &&  CountdownTimer(Exactly, 0))
		{// end shooting
			gameState = 40;
		}

	}
	if(gameState == 40)
	{// next
		for(var i=0; i<6; i++){
			tankAim.clearAngle(i);
		}
		turnPointer = (turnPointer+1)%6;
		gameState = 20;
	}
	if(gameState >= 20) 
	{
		playerDeath();
		//if(RELEASE) teamVictory();
		if(RELEASE) indiVictory();
	}
	EUDPlayerLoop()();
	if(getcurpl() != $P8)
		SetAllianceStatus($Force1, Ally);
	EUDEndPlayerLoop();
	// 물리엔진
	tankMain.renderTank();	 
	tankBullet.renderBullet(wind);
	// 바람, 아이템, 총알 별 데미지 및 이펙트
}

function indiVictory()
{
	if(Command($Force1, Exactly, 1, '(men)'))
	{
		EUDPlayerLoop()();
		Victory();
		EUDEndPlayerLoop();
	}
}
function teamVictory()
{
	if(Command($Force1, Exactly, 0, '(men)') || Command($Force3, Exactly, 0, '(men)'))
	{
		EUDPlayerLoop()();
		Victory();
		EUDEndPlayerLoop();
	}
}

function playerDeath()
{
	for(var i=0; i<6; i++)
	{
		if(turnArray[i] != 7)
		{
			if(Bring(turnArray[i], Exactly, 0, '(men)', $L('PlayArea')+1))
			{
				if(tankMain.checkDeath(turnArray[i]))
				{
					const chatPtr = dwread_epd(EPD(0x640B58));
					ct.printP(ct.cp, "\x13\x01사망");
					getPlayerText(turnArray[i]);
					dwwrite_epd(EPD(0x640B58), chatPtr);
					//remove Epd
					tankMain.removeTank(turnArray[i]);
					turnArray[i] = 7;
				}
			}
		}
	}
}

function getRandomTurn()
{
	for(var i = 0; i<6; i++)
	{
		const rn = rand()%6;
		if(rn == i)
		{
			continue;
		}
		else
		{//swap
			const temp = turnArray[i];
			turnArray[i] = turnArray[rn];
			turnArray[rn] = temp;
		}
	}
	for(var j=0; j<6; j++)
	{
		//empty player
		if(Deaths(turnArray[j], Exactly, 0, header.tankType))
		{
			turnArray[j] = 7;
		}
	}
	return 1;
}

function gameInfo(targetPlayer)
{
	ct.printP(targetPlayer, "\x07[조작법]");
	ct.printP(targetPlayer, "\x07[I],[K]: \x01각도");
	ct.printP(targetPlayer, "\x07[J]: \x01발사파워 (꾹 누름)");
	ct.printP(targetPlayer, "\x07[L]: \x01무기교체 ");
	ct.printP(targetPlayer, "\x01이동: \x01그냥 우클릭 무브해");

}
function getPlayerText(turnPlayer)
{
	//const chatPtr = dwread_epd(EPD(0x640B58));
  	//ct.printP(ct.cp, ""); // ct.cp는 모두에게 출력
  	if(turnPlayer == 0) ct.printP(ct.cp, "\x13\x06빨강\x01");
  	else if(turnPlayer == 1) ct.printP(ct.cp, "\x13\x0e파랑\x01");
  	else if(turnPlayer == 2) ct.printP(ct.cp, "\x13\x0f연두\x01");
  	else if(turnPlayer == 3) ct.printP(ct.cp, "\x13\x10보라\x01");
  	else if(turnPlayer == 4) ct.printP(ct.cp, "\x13\x11주황\x01");
  	else if(turnPlayer == 5) ct.printP(ct.cp, "\x13\x15갈색\x01");
  	else ct.printP(ct.cp, "\x13\x01예비");
	//dwwrite_epd(EPD(0x640B58), chatPtr);
}

function displayWind(windVal)
{
	const wind = 5 - windVal;
	if(wind == -5) ct.printP(ct.cp, "\x12바람: \x0e◀◀◀◀◀▷▷▷▷▷\x01");
	else if(wind == -4) ct.printP(ct.cp, "\x12바람: \x0e◁◀◀◀◀▷▷▷▷▷\x01");
	else if(wind == -3) ct.printP(ct.cp, "\x12바람: \x0e◁◁◀◀◀▷▷▷▷▷\x01");
	else if(wind == -2) ct.printP(ct.cp, "\x12바람: \x0e◁◁◁◀◀▷▷▷▷▷\x01");
	else if(wind == -1) ct.printP(ct.cp, "\x12바람: \x0e◁◁◁◁◀▷▷▷▷▷\x01");
	else if(wind == 0) ct.printP(ct.cp, "\x12바람: \x0e◁◁◁◁◁▷▷▷▷▷\x01");
	else if(wind == 1) ct.printP(ct.cp, "\x12바람: \x0e◁◁◁◁◁▶▷▷▷▷\x01");
	else if(wind == 2) ct.printP(ct.cp, "\x12바람: \x0e◁◁◁◁◁▶▶▷▷▷\x01");
	else if(wind == 3) ct.printP(ct.cp, "\x12바람: \x0e◁◁◁◁◁▶▶▶▷▷\x01");
	else if(wind == 4) ct.printP(ct.cp, "\x12바람: \x0e◁◁◁◁◁▶▶▶▶▷\x01");
	else if(wind == 5) ct.printP(ct.cp, "\x12바람: \x0e◁◁◁◁◁▶▶▶▶▶\x01");
}
